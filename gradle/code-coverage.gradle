apply plugin: 'jacoco'

jacoco {
    toolVersion = deps.versions.jacoco
    reportsDir = file("$buildDir/codeCoverageReports")
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task debugCombinedCodeCoverageReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    group = "Code coverage"
    description = "Generate coverage reports that combine both Unit and UI tests for debug builds"

    reports {
        html.enabled = true
    }

    def exclusions = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*'
    ]

    def javaClasses = fileTree(dir: "${buildDir}/intermediates/javac/debug/classes",
            excludes: exclusions)
    def kotlinClasses = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug",
            excludes: exclusions)

    classDirectories.setFrom(files([javaClasses, kotlinClasses]))

    def javaSourceDirectory = "$project.projectDir/src/main/java"
    def kotlinSourceDirectory = "$project.projectDir/src/main/kotlin"

    sourceDirectories.setFrom(files([javaSourceDirectory, kotlinSourceDirectory]))

    def unitTestsData = fileTree(dir: "${buildDir}/jacoco/", includes: ["**/*.exec"])
    def androidTestsData = fileTree(dir: "${buildDir}/outputs/code_coverage/debugAndroidTest/connected/", includes: ["**/*.ec"])

    executionData(files([unitTestsData, androidTestsData]))
}
